
(extension version)
	Debugging extension 1.1.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Informative actions

(understand command [scope])
(perform [scope])
	(scope)

(understand command [allrooms])
(perform [allrooms])
	All rooms: (line)
	(exhaust) {
		*(room $Room)
		$Room: (The $Room)
		(if) ($Room is visited) (then) \(visited\) (endif)
		(line)
	}

(understand command [meminfo])
(perform [meminfo])
	(display memory statistics)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Manipulative actions

(understand [teleport | $Words] as [teleport $Room])
	(determine object $Room)
		*(room $Room)
	(from words)
		*(dict $Room)
	(matching all of $Words)

(describe action [teleport $Room])
	teleport to (the full $Room)

~(refuse [teleport $])

(perform [teleport $Room])
	(inhibit next tick)
	(enter $Room)

(understand [purloin | $Words] as [purloin $Obj])
	(determine object $Obj)
		*(item $Obj)
	(from words)
		*(dict $Obj)
	(matching all of $Words)

~(refuse [purloin $])

(perform [purloin $Obj])
	(inhibit next tick)
	(current player $Player)
	(now) ($Obj is #heldby $Player)
	(now) ($Obj is handled)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Action tracing

(understand command [actions on])
(perform [actions on])
	(actions on)

(understand command [actions off])
(perform [actions off])
	(actions off)
